// Provides the `Hash` trait and related traits, and basic hashing functions.
module Hash;

// Hashable types.
trait a : Hash {
    hash : a -> U64;
}

// Hashable and comparable types.
// 
// Type implementing `HashKey` can be keys in a hash table.
trait HashKey = Hash + Eq;

// Combines two hashes.
// 
// Using method described in https://stackoverflow.com/questions/5889238/why-is-xor-the-default-way-to-combine-hashes.
combine_hash : U64 -> U64 -> U64;
combine_hash = |a, b| (
    a.bit_xor(b + 0x517cc1b727220a95_U64 + (a.shift_left(6_U64)) + (a.shift_right(2_U64)))
);

impl [a : Hash] Array a : Hash {
    hash = |arr| (
        let len = arr.get_size;
        loop((0, 5381_U64), |(i, h)| (
            if i == len { break $ h };
            let h = combine_hash(h, arr.@(i).hash);
            continue $ (i+1, h)
        ))
    );
}

impl String : Hash {
    hash = |s| s.@_data.hash;
}

impl U8 : Hash {
    hash = |x| x.to_U64.hash;
}

impl U64 : Hash {
    hash = |x| (
        // https://stackoverflow.com/questions/664014/what-integer-hash-function-are-good-that-accepts-an-integer-hash-key
        let x = x.bit_xor(x.shift_right(30_U64)) * 13787848793156543929_U64;
        let x = x.bit_xor(x.shift_right(27_U64)) * 10723151780598845931_U64;
        x.bit_xor(x.shift_right(31_U64))
    );
}

impl I64 : Hash {
    hash = I64::to_U64 >> hash;
}

impl [a : Hash, b : Hash] (a, b) : Hash {
    hash = |(a, b)| combine_hash(a.hash, b.hash);
}